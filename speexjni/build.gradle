apply plugin: 'com.android.model.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = '1.0'
group = 'com.github.morgenroth.codecs'

def sharedVersion = version

model {
    android {
        compileSdkVersion = 22
        buildToolsVersion = "23.0.2"

        defaultConfig.with {
            minSdkVersion.apiLevel = 8
            targetSdkVersion.apiLevel = 23
            versionCode = 1
            versionName = sharedVersion

            buildConfigFields.with {
                create() {
                    type = "int"
                    name = "VALUE"
                    value = "1"
                }
            }
        }
    }
    android.ndk {
        moduleName = "libspeex"
        toolchain = "clang"
        toolchainVersion = "3.5"
        CFlags.addAll(["-DFIXED_POINT", "-DUSE_KISS_FFT", "-DEXPORT=''", "-UHAVE_CONFIG_H"])
        CFlags.add("-I${file("src/main/jni/include")}".toString())
        ldLibs.add("log")
    }
    android.buildTypes {
        release {
            minifyEnabled = false
            proguardFiles.add(file('proguard-android.txt'))
        }
    }
}

dependencies {
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                name "speex-android"
                description "speex library for Android"
                url "http://github.com/morgenroth/speex-android"

                developers {
                    developer {
                        id "morgenroth"
                        name "Johannes Morgenroth"
                        email "jm@m-network.de"
                    }
                }
                scm {
                    connection "https://github.com/morgenroth/speex-android.git"
                    developerConnection "https://github.com/morgenroth/speex-android.git"
                    url "https://github.com/morgenroth/speex-android"
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.api_key')
    configurations = ['archives']
    publish = true
    pkg {
        userOrg = 'morgenroth'
        repo = 'maven'
        name = 'speexjni'
        websiteUrl = 'https://github.com/morgenroth/speex-android'
        issueTrackerUrl = 'https://github.com/morgenroth/speex-android/issues'
        vcsUrl = 'https://github.com/morgenroth/speex-android.git'
        labels = ['speex']
        licenses = ['GPL-2.0']
        version {
            name=sharedVersion
            vcsTag=sharedVersion
        }
    }
}

